
require-macros:
   "../src/struct" ->
      struct
   earl-mocha ->
      describe, it, before, after, before-each, after-each
      xdescribe, xit
      assert, asserts
      expect-error

require:
   "../src/index" ->
      make-struct, read-proxy, ID
   "../src/reactive" ->
      Reactive, reactive-function, System


struct Person:
   name, age, mother, father
   say-name{} =
      'My name is {@name}!'
   get-older{n = 1} =
      @age += n

alice = Person{name = .alice, age = 51}
bob = Person{name = .bob, age = 56}
clara = Person{name = .clara, age = 20, mother = alice, father = bob}

aid = alice[ID]
bid = bob[ID]
cid = clara[ID]

count-calls{f} =
   rval{*args} =
      rval.count += 1
      f.apply{this, args}
   rval.count = 0
   rval


describe "utilities":

   it "count-calls":
      count-calls! f{x, y} = x + y
      assert f{10, 20} == 30
      assert f.count == 1
      assert f{7, 8} == 15
      assert f.count == 2


;; count-call! reactive-function! say-name{person} =
;;    {greeting = 'Hello, my name is {person.name}!'}

;; count-call! reactive-function! say-parent-names{person} =



describe "Reactive functions":

   before-each:

      count-calls! reactive-function! @say-name{person} =
         {greeting = 'Hello, my name is {person.name}!'}

      count-calls! reactive-function! @say-parent-names{person} =
         {mother = @say-name{person.mother}
          father = @say-name{person.father}}


   it "called through System":
      sys = System{clara, @say-name}
      assert sys.value == {greeting = 'Hello, my name is clara!'}

   it "can be updated":

      sys = System{clara, @say-name}
      assert sys.value == {greeting = 'Hello, my name is clara!'}

      sys.transact with {clara} ->
         clara.name = "clairette"
      assert sys.value == {greeting = 'Hello, my name is clairette!'}

   it "is parsimonious":

      assert @say-name.orig.count == 0

      ;; Called to initialize
      sys = System{clara, @say-name}
      assert @say-name.orig.count == 1

      ;; Change is irrelevant because @say-name does not use age,
      ;; so @say-name should not be called again.
      sys.transact with {clara} ->
         clara.age = 21
      assert @say-name.orig.count == 1

      ;; Change is relevant, @say-name should be called
      sys.transact with {clara} ->
         clara.name = "clairette"
      assert @say-name.orig.count == 2

      ;; Irrelevant change again
      sys.transact with {clara} ->
         clara.mother = clara.father
      assert @say-name.orig.count == 2

      ;; Relevant change again
      sys.transact with {clara} ->
         clara.name = "bob"
      assert @say-name.orig.count == 3

   it "composes":
      sys = System{clara, @say-parent-names}

      assert sys.value == {
         mother = {greeting = 'Hello, my name is alice!'}
         father = {greeting = 'Hello, my name is bob!'}
      }

   it "compositions can be modified":

      sys = System{clara, @say-parent-names}

      sys.transact with {clara} ->
         clara.mother.name = .balice

      assert sys.value == {
         mother = {greeting = 'Hello, my name is balice!'}
         father = {greeting = 'Hello, my name is bob!'}
      }

   it "compositions can be modified and are parsimonious":

      assert @say-parent-names.orig.count == 0
      assert @say-name.orig.count == 0

      sys = System{clara, @say-parent-names}

      assert @say-parent-names.orig.count == 1
      assert @say-name.orig.count == 2

      sys.transact with {clara} ->
         clara.mother.name = .balice

      assert @say-parent-names.orig.count == 1
      assert @say-name.orig.count == 3

      sys.transact with {clara} ->
         clara.father.age = 88

      assert @say-parent-names.orig.count == 1
      assert @say-name.orig.count == 3

      sys.transact with {clara} ->
         clara.father.name = .gob
         clara.mother = clara.father

      assert @say-parent-names.orig.count == 2
      assert @say-name.orig.count == 4

   describe "miscellaneous tests":

      it "swap":
         sys = System{clara, @say-parent-names}
         sys.transact with {clara} ->
            {=> mother, => father} = clara
            {clara.mother, clara.father} = {father, mother}
         assert sys.value == {
            mother = {greeting = 'Hello, my name is bob!'}
            father = {greeting = 'Hello, my name is alice!'}
         }

      it "change all":
         sys = System{clara, @say-parent-names}
         sys.transact with {clara} ->
            {=> mother, => father} = clara
            mother.name = .yann
            father.name = .zoe
            {clara.mother, clara.father} = {father, mother}
         assert sys.value == {
            mother = {greeting = 'Hello, my name is zoe!'}
            father = {greeting = 'Hello, my name is yann!'}
         }

      it "deep change":
         sys = System{clara, @say-parent-names}
         sys.transact with {clara} ->
            clara.mother.name = .gertrude
         assert sys.value == {
            mother = {greeting = 'Hello, my name is gertrude!'}
            father = {greeting = 'Hello, my name is bob!'}
         }
